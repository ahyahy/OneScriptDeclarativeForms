// Сценарий запускает файлы "*.os" из подкаталога Среда
// Условия запуска:
// На уровне этого сценария должна лежать библиотека OneScriptForms.dll
// В подкаталоге Среда должна лежать библиотека DeclarativeForms.dll
// При тестировании в браузере одна вкладка пусть будет создана.

Перем Ф, СписокОшибок, Счет, Процесс1, Таймер, ТекПуть;

Функция РазобратьСтроку(Строка, Разделитель)
	Стр = СтрЗаменить(Строка, Разделитель, Символы.ПС);
	М = Новый Массив;
	Если ПустаяСтрока(Стр) Тогда
		Возврат М;
	КонецЕсли;
	Для Ч = 1 По СтрЧислоСтрок(Стр) Цикл
		М.Добавить(СтрПолучитьСтроку(Стр, Ч));
	КонецЦикла;
	Возврат М;
КонецФункции

Функция СтрНайтиМежду(СтрПараметр, Фрагмент1 = Неопределено, Фрагмент2 = Неопределено, ИсключитьФрагменты = Истина, БезНаложения = Истина)
	//Стр - исходная строка
	//Фрагмент1 - подстрока поиска от которой ведем поиск
	//Фрагмент2 - подстрока поиска до которой ведем поиск
	//ИсключитьФрагменты - не включать Фрагмент1 и Фрагмент2 в результат
	//БезНаложения - в результат не будут включены участки, содержащие другие найденные участки, удовлетворяющие переданным параметрам
	//функция возвращает массив строк
	Стр = СтрПараметр;
	М = Новый Массив;
	Если (Фрагмент1 <> Неопределено) и (Фрагмент2 = Неопределено) Тогда
		Позиция = Найти(Стр, Фрагмент1);
		Пока Позиция > 0 Цикл
			М.Добавить(?(ИсключитьФрагменты, Сред(Стр, Позиция + СтрДлина(Фрагмент1)), Сред(Стр, Позиция)));
			Стр = Сред(Стр, Позиция + 1);
			Позиция = Найти(Стр, Фрагмент1);
		КонецЦикла;
	ИначеЕсли (Фрагмент1 = Неопределено) и (Фрагмент2 <> Неопределено) Тогда
		Позиция = Найти(Стр, Фрагмент2);
		СуммаПозиций = Позиция;
		Пока Позиция > 0 Цикл
			М.Добавить(?(ИсключитьФрагменты, Сред(Стр, 1, СуммаПозиций - 1), Сред(Стр, 1, СуммаПозиций - 1 + СтрДлина(Фрагмент2))));
			Позиция = Найти(Сред(Стр, СуммаПозиций + 1), Фрагмент2);
			СуммаПозиций = СуммаПозиций + Позиция;
		КонецЦикла;
	ИначеЕсли (Фрагмент1 <> Неопределено) и (Фрагмент2 <> Неопределено) Тогда
		Позиция = Найти(Стр, Фрагмент1);
		Пока Позиция > 0 Цикл
			Стр2 = ?(ИсключитьФрагменты, Сред(Стр, Позиция + СтрДлина(Фрагмент1)), Сред(Стр, Позиция));
			Позиция2 = Найти(Стр2, Фрагмент2);
			СуммаПозиций2 = Позиция2;
			Пока Позиция2 > 0 Цикл
				Если БезНаложения Тогда
					Если Найти(Сред(Стр2, 1, СуммаПозиций2 - 1), Фрагмент2) = 0 Тогда
						М.Добавить("" + ?(ИсключитьФрагменты, Сред(Стр2, 1, СуммаПозиций2 - 1), Сред(Стр2, 1, СуммаПозиций2 - 1 + СтрДлина(Фрагмент2))));
					КонецЕсли;
				Иначе
					М.Добавить("" + ?(ИсключитьФрагменты, Сред(Стр2, 1, СуммаПозиций2 - 1), Сред(Стр2, 1, СуммаПозиций2 - 1 + СтрДлина(Фрагмент2))));
				КонецЕсли;
				Позиция2 = Найти(Сред(Стр2, СуммаПозиций2 + 1), Фрагмент2);
				СуммаПозиций2 = СуммаПозиций2 + Позиция2;
			КонецЦикла;
			Стр = Сред(Стр, Позиция + 1);
			Позиция = Найти(Стр, Фрагмент1);
		КонецЦикла;
	КонецЕсли;
	
	Возврат М;
КонецФункции//СтрНайтиМежду

Функция Команда1(ИмяФайла, Аргументы, Имя)
	ИнформацияЗапускаПроцесса1 = Ф.ИнформацияЗапускаПроцесса();
	ИнформацияЗапускаПроцесса1.ИмяФайла = ИмяФайла;
	ИнформацияЗапускаПроцесса1.Аргументы = Аргументы;
	ИнформацияЗапускаПроцесса1.СоздатьБезОкна = Истина;
	ИнформацияЗапускаПроцесса1.ИспользоватьОболочку = Ложь;
	ИнформацияЗапускаПроцесса1.СтильОкна = Ф.СтильОкнаПроцесса.Скрытое;
	ИнформацияЗапускаПроцесса1.ПеренаправитьСтандартныйВывод = Истина;

	Процесс1 = Ф.Процесс();
	Процесс1.НачальнаяИнформация = ИнформацияЗапускаПроцесса1;
	Процесс1.Начать();
	Если Не Процесс1.Завершен Тогда
		Стр1 = Процесс1.СтандартныйВывод.ПрочитатьСтроку();
		Пока Стр1 <> Неопределено Цикл
			Сообщить("" + Стр1);
			Стр3 = "" + Стр1;
			Если (СтрНайти(Стр3, "!!!") > 0) 
				или (СтрНайти(Стр3, "{") > 0)  
				или (СтрНайти(Стр3, "Script file is not found") > 0)  
				или (СтрНайти(Стр3, "При присвоении значения свойству") > 0)  
				или (СтрНайти(Стр3, "Ошибка") > 0)  
				Тогда
				СписокОшибок.Добавить(Стр3);
			КонецЕсли;
			// Сообщить(Стр3 + "  " + Имя + Символы.ПС);
			Стр1 = Процесс1.СтандартныйВывод.ПрочитатьСтроку();
		КонецЦикла;
	Иначе
		Сообщить("Процесс1 завершен");
	КонецЕсли;
КонецФункции//Команда1

Процедура ТестыДекларФорм()
	ВыбранныеФайлы = НайтиФайлы(ОбъединитьПути(ТекПуть,"Среда"), "*", Ложь);
	// Пути файлов нужно сложить в список и отсортировать по алфавиту, а то линукс берет не по алфавиту.
	СписокФайлов = Новый СписокЗначений();
	Для А = 0 По ВыбранныеФайлы.ВГраница() Цикл //ВыбранныеФайлы.ВГраница()
		Если СтрНайти((Новый СистемнаяИнформация()).ВерсияОС, "Windows") > 0 Тогда
		Иначе
			Если (СтрНайти(ВыбранныеФайлы[А].Имя, "Нажатие_") > 0) и (СтрНайти(ВыбранныеФайлы[А].Имя, "_метод.os") > 0) Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;		
		СписокФайлов.Добавить(ВыбранныеФайлы[А].ПолноеИмя, ВыбранныеФайлы[А].Имя);
	КонецЦикла;
	СписокФайлов.СортироватьПоЗначению();
	
	Для А = 0 По СписокФайлов.Количество() - 1 Цикл
		ПолноеИмя = СписокФайлов.Получить(А).Значение;
		Имя = СписокФайлов.Получить(А).Представление;
		Счет = Счет + 1;
		Сообщить("" + Счет + " (" + Ф.Математика().Окр(((ТекущаяУниверсальнаяДатаВМиллисекундах() - Таймер)/1000)/60, 2) + " мин." + " " + (А + 1) + " из " + 
		СписокФайлов.Количество() + ") " + ПолноеИмя);
		Если СтрНайти((Новый СистемнаяИнформация()).ВерсияОС, "Windows") > 0 Тогда
			Команда1("""C:\Program Files\OneScript\bin\oscript.exe""", ПолноеИмя, Имя);
		Иначе
			Команда1("oscript", ПолноеИмя, Имя);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры//ТестыДекларФорм

Таймер = ТекущаяУниверсальнаяДатаВМиллисекундах();
ТекПуть = Новый Файл(ТекущийСценарий().Источник).Путь;

ПодключитьВнешнююКомпоненту(ОбъединитьПути(ТекПуть,"OneScriptForms.dll"));
Ф = Новый ФормыДляОдноСкрипта();
СписокОшибок = Новый СписокЗначений;

Счет = 0;

ТестыДекларФорм();
Сообщить("==============================================================================================" + Символы.ПС);
Сообщить("Тест выполнен за: " + ((ТекущаяУниверсальнаяДатаВМиллисекундах()-Таймер)/1000)/60 + " мин." + " " + ТекущаяДата() + Символы.ПС);
Сообщить("Ошибок = " + СписокОшибок.Количество() + Символы.ПС);
Для А = 0 По СписокОшибок.Количество() - 1 Цикл
	Сообщить("" + СписокОшибок.Получить(А).Значение);
КонецЦикла;
